# 3.6: Motoko level 3

## Overview

In the previous module, [3.5: Identities and authentication](3.5-identities-and-auth.md) we covered different forms of identities and authentication on the IC. In this Motoko level, we'll look into caller identification, adding access control with identities, plus other non-identity related functionalities such as pattern matching and periodic tasks. 

## Principals and caller identification

Motoko's shared functions support a form of caller identification, allowing for the principal associated with the caller of a function be returned. Recall that a principal is a unique identifier associated with the user or canister. Principals can be used to implement basic access control for an application. 

In Motoko, recall that the `shared` keyword is used to declare a shared function, and that a **shared function** immediately returns a value known as a **future**. This future value indicates that an asynchronous value is expected to be returned. A shared function's arguments and return value(s) must also be shared types. Shared functions can declare an optional parameter of type `{caller : Principal}`. We covered shared functions in more detail in [2.6: Motoko level 2](../level-2/2.6-motoko-lvl2.md)

To demonstrate this, consider the following function:

```motoko
shared(msg) func inc() : async () {
  // ... msg.caller ...
}
```

In this basic demonstration, the shared function `inc()` specifies a `msg` parameter, a record, and the `msg.caller` accesses the `principal` field of `msg`. Calls to the `inc()` function do not change. When the call is made, the caller's `principal` is provided by the system, not the user, so the principal cannot be forged to spoofed maliciously. 

Additionally, the caller of an actor class can be called using the same syntax on the actor class declaration, such as:

```motoko
shared(msg) actor class Counter(init : Nat) {
  // ... msg.caller ...
}
```

To further this example, imagine a scenario where you have the `Counter` actor that we used in a previous example that you want to restrict access to. We can modify the original example so that only the `principal` that installed the actor can modify the actor by recording the `principal` that installed the actor and binding it to an `owner` variable, then checking that the caller of each method is equal to the owner variable:

```motoko
shared(msg) actor class Counter(init : Nat) {

  let owner = msg.caller;

  var count = init;

  public shared(msg) func inc() : async () {
    assert (owner == msg.caller);
    count += 1;
  };

  public func read() : async Nat {
    count
  };

  public shared(msg) func bump() : async Nat {
    assert (owner == msg.caller);
    count := 1;
    count;
  };
}
```

In this code, the `assert (owner == msg.caller)` expression causes the functions `inc()` and `bump()` to trap if the call is unauthorized, preventing any modification of the count variable while the `read()` function permits any caller.

## Adding access control with identities

Now, let's look at creating a sample dapp that uses role-based permissions to control different operations that users can perform. 

In this example, we'll create a simple app that displays a different greeting for users that have been assigned different roles. We'll use three roles: `owner`, `admin`, and `authorized`:

- Users who have the `admin` role will receive the greeting, "You have a role with administrative privileges."

- Users who have the `authorized` role will receive the greeting, "Are you enjoying your developer journey?".

- Users who are not assigned a role will receive the greeting, "Nice to meet you!".

Only the `principal` that initialized the canister is assigned the `owner` role, and only principals with the `owner` and `admin` roles can assign roles to others.

### Prerequisites

Before you start, verify that you have set up your developer environment according to the instructions in [0.3: Developer environment setup](../level-0/03-dev-env.md).

### Creating a new project

To get started, create a new project in your working directory. Open a terminal window, navigate into your working directory (`developer_journey`), then use the commands:

```
dfx start --clean --background
dfx new access_hello
cd access_hello
```

First, let's create a new identity to use. We'll call this identity 'owner':

```
dfx identity new owner
```

Now, let's write the code that sets up our simple access control app. Open the `src/access_hello_backend/main.mo` file in your code editor and replace the existing content with the following pieces of code:

Start by importing the base modules:

```motoko
import AssocList "mo:base/AssocList";
import Error "mo:base/Error";
import List "mo:base/List";
```

Then, declare your shared actor class which defines three role-based greetings to display using an if/else statement:

```motoko
shared({ caller = initializer }) actor class() {

    public shared({ caller }) func greet(name : Text) : async Text {
        if (has_permission(caller, #assign_role)) {
            return "Hello, " # name # ". You have a role with administrative privileges."
        } else if (has_permission(caller, #lowest)) {
            return "Welcome, " # name # ". You have an authorized account. Would you like to play a game?";
        } else {
            return "Greetings, " # name # ". Nice to meet you!";
        }
    };
```

Next, define the custom types `Role` and `Permission`:

```motoko
    public type Role = {
        #owner;
        #admin;
        #authorized;
    };

    public type Permission = {
        #assign_role;
        #lowest;
    };
```

Then, define stable variables to store a list of principals and their associated role:

```motoko
    private stable var roles: AssocList.AssocList<Principal, Role> = List.nil();
    private stable var role_requests: AssocList.AssocList<Principal, Role> = List.nil();
```

Create a function that checks if principal `a` equals principal `b`:

```motoko
    func principal_eq(a: Principal, b: Principal): Bool {
        return a == b;
    };
```

Define a function to get the principal's current role, first by checking if the principal is the initializer of the canister, then by checking the list that stores principals and their associated role:

```motoko
    func get_role(pal: Principal) : ?Role {
        if (pal == initializer) {
            ?#owner;
        } else {
            AssocList.find<Principal, Role>(roles, pal, principal_eq);
        }
    };
```


Then, determine if a principal has a role with permissions:

```motoko
    func has_permission(pal: Principal, perm : Permission) : Bool {
        let role = get_role(pal);
        switch (role, perm) {
            case (?#owner or ?#admin, _) true;
            case (?#authorized, #lowest) true;
            case (_, _) false;
        }
    };
```

Define a function that rejects any unauthorized principals:

```motoko
    func require_permission(pal: Principal, perm: Permission) : async () {
        if ( has_permission(pal, perm) == false ) {
            throw Error.reject( "unauthorized" );
        }
    };
```

Define a function to assign a new role to a principal:

```motoko
    public shared({ caller }) func assign_role( assignee: Principal, new_role: ?Role ) : async () {
        await require_permission( caller, #assign_role );

        switch new_role {
            case (?#owner) {
                throw Error.reject( "Cannot assign anyone to be the owner" );
            };
            case (_) {};
        };
        if (assignee == initializer) {
            throw Error.reject( "Cannot assign a role to the canister owner" );
        };
        roles := AssocList.replace<Principal, Role>(roles, assignee, principal_eq, new_role).0;
        role_requests := AssocList.replace<Principal, Role>(role_requests, assignee, principal_eq, null).0;
    };

    public shared({ caller }) func request_role( role: Role ) : async Principal {
        role_requests := AssocList.replace<Principal, Role>(role_requests, caller, principal_eq, ?role).0;
        return caller;
    };
```

Return the principal of the message caller/user identity:

```motoko
    public shared({ caller }) func callerPrincipal() : async Principal {
        return caller;
    };
```


Return the role of the message caller/user identity:

```motoko
    public shared({ caller }) func my_role() : async ?Role {
        return get_role(caller);
    };

    public shared({ caller }) func my_role_request() : async ?Role {
        AssocList.find<Principal, Role>(role_requests, caller, principal_eq);
    };

    public shared({ caller }) func get_role_requests() : async List.List<(Principal,Role)> {
        await require_permission( caller, #assign_role );
        return role_requests;
    };

    public shared({ caller }) func get_roles() : async List.List<(Principal,Role)> {
        await require_permission( caller, #assign_role );
        return roles;
    };
};
```

In total, the code in the `src/access_hello_backend/main.mo` file should be:

```motoko
import AssocList "mo:base/AssocList";
import Error "mo:base/Error";
import List "mo:base/List";

shared({ caller = initializer }) actor class() {

    public shared({ caller }) func greet(name : Text) : async Text {
        if (has_permission(caller, #assign_role)) {
            return "Hello, " # name # ". You have a role with administrative privileges."
        } else if (has_permission(caller, #lowest)) {
            return "Welcome, " # name # ". You have an authorized account. Would you like to play a game?";
        } else {
            return "Greetings, " # name # ". Nice to meet you!";
        }
    };

    public type Role = {
        #owner;
        #admin;
        #authorized;
    };

    public type Permission = {
        #assign_role;
        #lowest;
    };

    private stable var roles: AssocList.AssocList<Principal, Role> = List.nil();
    private stable var role_requests: AssocList.AssocList<Principal, Role> = List.nil();

    func principal_eq(a: Principal, b: Principal): Bool {
        return a == b;
    };

    func get_role(pal: Principal) : ?Role {
        if (pal == initializer) {
            ?#owner;
        } else {
            AssocList.find<Principal, Role>(roles, pal, principal_eq);
        }
    };

    func has_permission(pal: Principal, perm : Permission) : Bool {
        let role = get_role(pal);
        switch (role, perm) {
            case (?#owner or ?#admin, _) true;
            case (?#authorized, #lowest) true;
            case (_, _) false;
        }
    };

    func require_permission(pal: Principal, perm: Permission) : async () {
        if ( has_permission(pal, perm) == false ) {
            throw Error.reject( "unauthorized" );
        }
    };

    public shared({ caller }) func assign_role( assignee: Principal, new_role: ?Role ) : async () {
        await require_permission( caller, #assign_role );

        switch new_role {
            case (?#owner) {
                throw Error.reject( "Cannot assign anyone to be the owner" );
            };
            case (_) {};
        };
        if (assignee == initializer) {
            throw Error.reject( "Cannot assign a role to the canister owner" );
        };
        roles := AssocList.replace<Principal, Role>(roles, assignee, principal_eq, new_role).0;
        role_requests := AssocList.replace<Principal, Role>(role_requests, assignee, principal_eq, null).0;
    };

    public shared({ caller }) func request_role( role: Role ) : async Principal {
        role_requests := AssocList.replace<Principal, Role>(role_requests, caller, principal_eq, ?role).0;
        return caller;
    };

    public shared({ caller }) func callerPrincipal() : async Principal {
        return caller;
    };

    public shared({ caller }) func my_role() : async ?Role {
        return get_role(caller);
    };

    public shared({ caller }) func my_role_request() : async ?Role {
        AssocList.find<Principal, Role>(role_requests, caller, principal_eq);
    };

    public shared({ caller }) func get_role_requests() : async List.List<(Principal,Role)> {
        await require_permission( caller, #assign_role );
        return role_requests;
    };

    public shared({ caller }) func get_roles() : async List.List<(Principal,Role)> {
        await require_permission( caller, #assign_role );
        return roles;
    };
};
```

The key components of this app are:

- A `greet` function that uses a message caller to determine the permissions that should be applied to a principal, then displays a greeting depending on the permissions associated with the caller. 

- Two custom types: `Roles` and `Permissions`. 

- An `assign_roles` function that enables the message caller to assign a role to a principal.

- A `callerPrincipal` function that returns the principal associated with an identity.

- A `my_role` function that returns the role associated with an identity.

Save these changes made to the `src/access_hello_backend/main.mo` file. 

Now let's deploy our canister with the command:

```
dfx deploy
```

Now that our canister is deployed, we can interact with it using different identities. First, let's review our canister's `owner` identity, which will be the current identity we've been using when we deployed the canister. Run the `whoami` command to see what the currently-active identity is:

```
dfx identity whoami
```




Before we create any additional identities, let’s review the principal identifiers associated with the owner identity and the cycles wallet for your owner identity. This owner identity is created when your canisters were deployed.

On the Internet Computer blockchain, a principal is the internal representative for a user, canister, node, or subnet. The textual representation for a principal is the external identifier you see displayed with working with the principal data type.

To review your current identity and principal, verify the currently-active identity by running the following command:

dfx identity whoami
The command displays output similar to the following:

owner
If the command displays anything other than default, you can switch to the default identity with the command:

dfx identity use owner
Check the principal for the default user identity by running the following command:

dfx identity get-principal
The command displays output similar to the following:

zen7w-sjxmx-jcslx-ey4hf-rfxdq-l4soz-7ie3o-hti3o-nyoma-nrkwa-cqe
Check the role associated with the default user identity by running the following command:

dfx canister call access_hello_backend my_role
The command displays output similar to the following:

(opt variant { owner })
Create a new user identity
To begin testing the access controls in our dapp, let’s create some new user identities and assign those users to different roles.

To create a new user identity, create a new administrative user identity by running the following command:

dfx identity new ic_admin
The command displays output similar to the following:

Your seed phrase for identity 'ic_admin': void thought frown volcano reject claw weekend waste boost mouse hen genre crop real tooth tray hero perfect swarm nest demand green when fade
This can be used to reconstruct your key in case of emergency, so write it down in a safe place.
Created identity: "ic_admin".
Call the my_role function to see that your new user identity has not been assigned to any role.

dfx --identity ic_admin canister call access_hello_backend my_role
The command displays output similar to the following:

Creating a wallet canister on the local network.
The wallet canister on the "local" network for user "ic_admin" is "ryjl3-tyaaa-aaaaa-aaaba-cai"
(null)
Switch your currently-active identity context to use the new ic_admin user identity and display the principal associated with the ic_admin user by running the following command:

dfx identity use ic_admin && dfx identity get-principal
The command displays output similar to the following:

Using identity: "ic_admin".
scc3r-hhpnt-264cn-t2ud3-sx74o-5txbl-arwi5-h7c4s-wx7zc-sl54q-dqe
Check the principal used to call the access_hello_backend canister by running the following command:

dfx canister call access_hello callerPrincipal
The command displays output similar to the following:

(principal "scc3r-hhpnt-264cn-t2ud3-sx74o-5txbl-arwi5-h7c4s-wx7zc-sl54q-dqe")
You will notice that the principal returned is the principal associated with the user context.

Assign a role to an identity
To assign the admin role to the ic_admin identity, switch your currently-active identity context to use the default user identity by running the following command:

dfx identity use default
Assign the ic_admin principal the admin role by running a command similar to the following using Candid syntax:

dfx canister call access_hello_backend assign_role '((principal "scc3r-hhpnt-264cn-t2ud3-sx74o-5txbl-arwi5-h7c4s-wx7zc-sl54q-dqe"),opt variant{admin})'
Be sure to replace the principal hash with the one returned by the dfx identity get-principal command for the ic_admin identity.

Optionally, you can rerun the command to call the my_role function to verify the role assignment.

dfx --identity ic_admin canister call access_hello_backend my_role
The command displays output similar to the following:

(opt variant { admin })
Call the greet function using the ic_admin user identity that you just assigned the admin role by running the following command:

dfx --identity ic_admin canister call access_hello_backend greet "Internet Computer Admin"
The command displays output similar to the following:

(
    "Hello, Internet Computer Admin. You have a role with administrative privileges.",
)
Add an authorized user identity
At this point, you have a owner user identity with the owner role and an ic_admin user identity with the admin role. Let’s add another user identity and assign it to the authorized role. For this example, however, we’ll use an environment variable to store the user’s principal.

To add a new authorized user identity, create a new authorized user identity by running the following command:

dfx identity new alice_auth
The command displays output similar to the following:

Your seed phrase for identity 'alice_auth': patch chronic blossom cup speed tape velvet coconut romance salad chicken castle refuse pepper first display jewel olive flight scene window diagram option shadow
This can be used to reconstruct your key in case of emergency, so write it down in a safe place.
Created identity: "alice_auth".
Switch your currently-active identity context to use the new alice_auth user identity by running the following command:

dfx identity use alice_auth
Store the principal for the alice_auth user in an environment variable by running the following command:

ALICE_ID=$(dfx identity get-principal)
You can verify the principal stored by running the following command:

echo $ALICE_ID
The command displays output similar to the following:

b5quc-npdph-l6qp4-kur4u-oxljq-7uddl-vfdo6-x2uo5-6y4a6-4pt6v-7qe
Use the ic_admin identity to assign the authorized role to alice_auth by running the following command:

dfx --identity ic_admin canister call access_hello_backend assign_role "(principal \"$ALICE_ID\", opt variant{authorized})"
Call the my_role function to verify the role assignment.

dfx --identity alice_auth canister call access_hello_backend my_role
The command displays output similar to the following:

(opt variant { authorized })
Call the greet function using the alice_auth user identity that you just assigned the authorized role by running the following command:

dfx canister call access_hello_backend greet "Alice"
The command displays output similar to the following:

(
    "Welcome, Alice. You have an authorized account. Would you like to play a game?",
)
Add an unauthorized user identity
You have now seen a simple example of creating users with specific roles and permissions. The next step is to create a user identity that is not assigned to a role or given any special permissions.

Check your currently-active identity, if needed, by running the following command:

dfx identity whoami
Create a new user identity by running the following command:

dfx identity new bob_standard
The command displays output similar to the following:

Your seed phrase for identity 'bob_standard': sting length child airport select tube crane render march flee notable sheriff gown fitness absorb shoot cry history brisk throw rapid accident helmet coast
This can be used to reconstruct your key in case of emergency, so write it down in a safe place.
Created identity: "bob_standard".
Store the principal for the bob_standard user in an environment variable by running the following command:

BOB_ID=$(dfx --identity bob_standard identity get-principal)
Attempt to use the bob_standard identity to assign a role.

dfx --identity bob_standard canister call access_hello_backend assign_role "(principal \"$BOB_ID\", opt variant{authorized})"
This command returns an unauthorized error.

Attempt to use the default user identity to assign bob_standard the owner role by running the following command:

dfx --identity default canister call access_hello_backend assign_role "(principal \"$BOB_ID\", opt variant{owner})"
This command fails because users cannot be assigned the owner role.

Call the greet function using the bob_standard user identity by running the following command:

dfx --identity bob_standard canister call access_hello_backend greet "Bob"
The command displays output similar to the following:

("Greetings, Bob. Nice to meet you!")
Set the user identity for multiple commands
So far, you have seen how to create and switch between user identities for individual commands. You can also specify a user identity you want to use, then run multiple commands in the context of that user identity.

List the user identities currently available by running the following command:

dfx identity list
The command displays output similar to the following with an asterisk indicating the currently-active user identity.

alice_auth
bob_standard
default *
ic_admin
owner
In this example, the default user identity is used unless you explicitly select a different identity.

Select a new user identity from the list and make it the active user context by running a command similar to the following:

dfx identity use ic_admin
The command displays output similar to the following:

Using identity: "ic_admin".
If you rerun the dfx identity list command, the ic_admin user identity displays an asterisk to indicate it is the currently active user context.

You can now run commands using the selected user identity without specifying --identity on the command-line.

## Pattern matching

Pattern matching is a language feature that makes it easy to both test and decompose structured data into its constituent parts. While most programming languages provide familiar ways to build structured data, pattern matching enables you to take apart structured data and bring its fragments into scope by binding them to the names you specify. Syntactically, the patterns resemble the construction of structured data, but generally appear in input-direction positions, such as in function argument positions, after the case keyword in switch expressions, and after let or var declarations.

Consider the following function call:

let name : Text = fullName({ first = "Jane"; mid = "M"; last = "Doe" });

This code constructs a record with three fields and passes it to the function fullName. The result of the call is named and brought into scope by binding it to the identifier name. The last, binding step is called pattern matching, and name : Text is one of the simplest forms of pattern. For instance, in the following implementation of the callee:

1
2
3
func fullName({ first : Text; mid : Text; last : Text }) : Text {
  first # " " # mid # " " # last
};

The input is an (anonymous) object, which is destructured into its three Text fields, whose values are bound to the identifiers first, mid and last. They can be freely used in the block that forms the body of the function. Above we have resorted to name punning (a form of aliasing) for object field patterns, using the name of a field to also name its contents. A more general form of field pattern allows the content to be named separately from the field, as in …​; mid = m : Text; …​. Here mid determines which field to match, and m names the content of that field within the scope of the pattern.

You can also use pattern matching to declare literal patterns, which look just like literal constants. Literal patterns are especially useful in switch expressions because they can cause the current pattern match to fail, and thus start to match the next pattern. For example:

1
2
3
4
5
switch ("Adrienne", #female) {
  case (name, #female) { name # " is a girl!" };
  case (name, #male) { name # " is a boy!" };
  case (name, _) { name # ", is a human!" };
}

will match the first case clause (because binding to the identifier name cannot fail and the shorthand variant literal #Female compares as equal), and evaluate to "Adrienne is a girl!". The last clause showcases the wildcard pattern _. It cannot fail, but won’t bind any identifier.
The last kind of pattern is the or pattern. As its name suggests, these are two or more patterns that are separated by the keyword or. Each of the sub-patterns must bind to the same set of identifiers, and is matched from left-to-right. An or pattern fails when its rightmost sub-pattern fails.



## Periodic tasks

Overview
Unlike other blockchains, the Internet Computer can automatically execute canister smart contracts after a specified delay or periodically.

There are two ways to schedule an automatic canister execution on the IC:

Timers: single-expiration or periodic canister calls with specified minimum timeout or interval.
Heartbeats: legacy periodic canister invocations with intervals close to the blockchain finalization rate (1s). Heartbeats are supported by the IC for backward compatibility and some very special use cases. Newly developed canisters should prefer using timers over the heartbeats.

Timers
Internet Computer canisters can set an arbitrary number of single-expiration or recurring timers. See the Timer.mo module in the base library.

A simple, contrived example is a periodic reminder, that logs a new-year's message:

import { print } = "mo:base/Debug";
import { abs } = "mo:base/Int";
import { now } = "mo:base/Time";
import { setTimer; recurringTimer } = "mo:base/Timer";

actor Reminder {

  let solarYearSeconds = 356_925_216;

  private func remind() : async () {
    print("Happy New Year!");
  };

  ignore setTimer(#seconds (solarYearSeconds - abs(now() / 1_000_000_000) % solarYearSeconds),
    func () : async () {
      ignore recurringTimer(#seconds solarYearSeconds, remind);
      await remind();
  });
}
The underlying mechanism is a canister global timer that, by default, is issued with appropriate callbacks from a priority queue maintained by the Motoko runtime.

The timer mechanism can be disabled completely by passing the -no-timer flag to moc.

When lower-level access to the canister global timer is desired, an actor can elect to receive timer expiry messages by declaring a system function, named timer. The function takes one argument (to re-set the global timer), and returns a future of unit type (async ()). If the timer system method is declared, the Timer.mo base library module may not function correctly and should not be used.

The following example of a global timer expiration callback gets called immediately after the canister starts (i.e. after install) and periodically every twenty seconds thereafter:

system func timer(setGlobalTimer : Nat64 -> ()) : async () {
  let next = Nat64.fromIntWrap(Time.now()) + 20_000_000_000;
  setGlobalTimer(next); // absolute time in nanoseconds
  print("Tick!");
}
